<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
 "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">

<!-- Generated by IDLdoc 3.4 on Fri Jul 29 15:39:40 2016 -->

<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
  <head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8"/>
    <title>matchall_sph.pro (Documentation for jbiu/)</title>

    
    <link rel="stylesheet" type="text/css" media="all"
          href="../idldoc-resources/main.css" />
    <link rel="stylesheet" type="text/css" media="print"
          href="../idldoc-resources/main-print.css" />
    

    <script type="text/javascript">
      function setTitle() {
        parent.document.title="matchall_sph.pro (Documentation for jbiu/)";
      }
    </script>
  </head>

  <body onload="setTitle();" id="root">
    <div class="content">
      <code class="source"><span class="comments">; for usemap=0, the mapping is just the inverse of array_indices.</span>
<span class="comments">; for usemap=1, uses the index in map that corresponds to the above</span>
<span class="comments">; (-1 if it doesn't occur)</span>
<a id="wsrad_map:source"></a>function wsrad_map, ngrid, a0, a1, a2, map, usemap
  index = a0 + ngrid*(a1 + ngrid*a2)
  if ~usemap then return, index
  result = value_locate(map, index)
  missing = where(map[result] ne index, nmissing)
  if nmissing gt 0 then result[missing]=-1
  return, result
end


<span class="comments">;+</span>
<span class="comments">; NAME:</span>
<span class="comments">;    MATCHALL_SPH</span>
<span class="comments">;</span>
<span class="comments">; PURPOSE:</span>
<span class="comments">;    Determines which of a set of angular coordinates on the sky (or on a</span>
<span class="comments">;    sphere) are within a given angular distance from each of a vector of</span>
<span class="comments">;    points. New optimized version that uses histograms based on 3D</span>
<span class="comments">;    locations on the unit sphere and borrows heavily from JD's MATCH_2D.</span>
<span class="comments">;</span>
<span class="comments">; CATEGORY:</span>
<span class="comments">;    Astro</span>
<span class="comments">;</span>
<span class="comments">; CALLING SEQUENCE:</span>
<span class="comments">;    Result = MATCHALL_SPH(Ra1, Dec1, Ra2, Dec2, Sphrad, Nwithin)</span>
<span class="comments">;</span>
<span class="comments">; INPUTS:</span>
<span class="comments">;    Ra1:     Vector of longitude coordinates, in degrees.</span>
<span class="comments">;</span>
<span class="comments">;    Dec1:    Vector of latitude coordinates, in degrees.</span>
<span class="comments">;</span>
<span class="comments">;    Ra2:     Vector of longitude coordinates, in degrees.</span>
<span class="comments">;</span>
<span class="comments">;    Dec2:    Vector of latitude coordinates, in degrees.</span>
<span class="comments">;</span>
<span class="comments">;    Sphrad:  Maximum angular distance, in degrees.</span>
<span class="comments">;</span>
<span class="comments">; OUTPUTS:</span>
<span class="comments">;    The function returns the list of indices of Ra2, Dec2 that lie within</span>
<span class="comments">;    Sphrad of each point Ra1,Dec1. The format of the returned array is</span>
<span class="comments">;    similar to the REVERSE_INDICES array from HISTOGRAM: the indices</span>
<span class="comments">;    into Ra2,Dec2 that are close enough to element i of Ra1,Dec1 are</span>
<span class="comments">;    contained in Result[Result[i]:Result[i+1]-1] (note, however, that</span>
<span class="comments">;    these indices are not guaranteed to be sorted). If there are no matches,</span>
<span class="comments">;    then Result[i] eq Result[i+1].</span>
<span class="comments">;</span>
<span class="comments">; OPTIONAL OUTPUTS:</span>
<span class="comments">;    Nwithin: A vector containing the number of matches for each of Ra1,Dec1.</span>
<span class="comments">;</span>
<span class="comments">; KEYWORD PARAMETERS:</span>
<span class="comments">;    DISTANCE:  Optional output containing the distances between each pair.</span>
<span class="comments">;               The distances are stored in the same order as the Result</span>
<span class="comments">;               array but starting at 0, i.e. if j is match number k to</span>
<span class="comments">;               element i then</span>
<span class="comments">;                   j = Result[Result[i]+k]</span>
<span class="comments">;               and the distance between points i and j is</span>
<span class="comments">;                   DISTANCE[Result[i]+k-Result[0]]</span>
<span class="comments">;</span>
<span class="comments">; EXAMPLE:</span>
<span class="comments">;    Note that the routine is similar to finding</span>
<span class="comments">;      WHERE(SPHDIST(Ra1,Dec1,Ra2,Dec2,/DEGREE) LE Sphrad, Nwithin)</span>
<span class="comments">;    for each element of Ra1 and Dec1, but is much more efficient.</span>
<span class="comments">;</span>
<span class="comments">;    Shows which random points are within 10 degrees of various coordinates:</span>
<span class="comments">;</span>
<span class="comments">;    seed=43</span>
<span class="comments">;    nrandcoords = 5000l</span>
<span class="comments">;    ra_randcoords = 360. * RANDOMU(seed, nrandcoords)</span>
<span class="comments">;    dec_randcoords = ASIN( 2*RANDOMU(seed, nrandcoords)-1 ) * !RADEG</span>
<span class="comments">;    ra_centers = 60. * FINDGEN(5)</span>
<span class="comments">;    dec_centers = [0., 45., 0., -45., 90.]</span>
<span class="comments">;    matches = MATCHALL_SPH(ra_centers, dec_centers, ra_randcoords, $</span>
<span class="comments">;      dec_randcoords, 10., nmatches)</span>
<span class="comments">;    plot, /iso, psym=3, ra_randcoords, dec_randcoords</span>
<span class="comments">;    oplot, psym=1, color=fsc_color('blue'), ra_centers, dec_centers</span>
<span class="comments">;    oplot, psym=3, color=fsc_color('red'), ra_randcoords[matches[6:*]], $</span>
<span class="comments">;      dec_randcoords[matches[6:*]]</span>
<span class="comments">;</span>
<span class="comments">; MODIFICATION HISTORY:</span>
<span class="comments">;    Written by:    Jeremy Bailin</span>
<span class="comments">;    10 June 2008   Public release in JBIU as WITHINSPHRAD</span>
<span class="comments">;    24 April 2009  Vectorized as WITHINSPHRAD_VEC</span>
<span class="comments">;    25 April 2009  Polished to improve memory use</span>
<span class="comments">;    9 May 2009     Radical efficiency re-write as WITHINSPHRAD_VEC3D borrowing</span>
<span class="comments">;                   heavily from JD Smith's MATCH_2D</span>
<span class="comments">;    13 May 2009    Removed * from LHS index in final remapping for speed</span>
<span class="comments">;    8 Sept 2010    Renamed MATCHALL_SPH and added DISTANCE keyword</span>
<span class="comments">;    9 Aug 2011     Bug fix: incorrect bin size caused occasional matches</span>
<span class="comments">;                   to be missed (thanks to J. Donley for reporting)</span>
<span class="comments">;-</span>
<a id="matchall_sph:source"></a>function matchall_sph, ra1, dec1, ra2, dec2, sphrad, nwithin, $
  distance=distance

if n_elements(ra2) ne n_elements(dec2) then $
  message, 'RA2 and DEC2 must have the same number of elements.'
if n_elements(ra1) ne n_elements(dec1) then $
  message, 'RA1 and DEC1 must have the same number of elements.'
if n_elements(sphrad) ne 1 then $
  message, 'SPHRAD must contain one element.'

n1 = n_elements(ra1)
n2 = n_elements(ra2)

<span class="comments">; figure out grid spacing</span>
<span class="comments">; simple approximation gives a maximum theta of 2 sqrt(2) / N for</span>
<span class="comments">; N cells. It can really be higher, so use a 50% buffer.</span>
<span class="comments">; Relevant theta is 2xsphrad.</span>
theta = 2. * sphrad / !radeg
ngrid = (floor(2. * sqrt(2) / (1.5 * theta)) + 1) > 2
gridlen = 2. / (ngrid-1)
<span class="comments">; shift edge of box</span>
minbox = -1. - 0.5*gridlen

<span class="comments">; create a mapping so we don't need to histogram the entire</span>
<span class="comments">; 3d space</span>
<span class="comments">; we only need to map the points that contain either an ra1,dec1</span>
<span class="comments">; pair or an ra2,dec2 pair</span>
xoff = (cos(ra1/!radeg)*cos(dec1/!radeg)-minbox)/gridlen
yoff = (sin(ra1/!radeg)*cos(dec1/!radeg)-minbox)/gridlen
zoff = (sin(dec1/!radeg)-minbox)/gridlen
xbin = floor(xoff)
ybin = floor(yoff)
zbin = floor(zoff)
x2bin = floor((cos(ra2/!radeg)*cos(dec2/!radeg)-minbox)/gridlen)
y2bin = floor((sin(ra2/!radeg)*cos(dec2/!radeg)-minbox)/gridlen)
z2bin = floor((sin(dec2/!radeg)-minbox)/gridlen)
indices = wsrad_map(ngrid,[xbin,x2bin],[ybin,y2bin],[zbin,z2bin],surfacemap,0)
indexsort = sort(indices)
surfacemap = indices[indexsort[uniq(indices[indexsort])]]
undefine, indexsort
undefine, indices

<span class="comments">; histogram points 2</span>
<span class="comments">; note the extra 0 out front - used so that when we look for bin "-1"</span>
<span class="comments">; (meaning irrelevant), we know there are 0 entries there.</span>
h=[0,histogram(wsrad_map(ngrid,x2bin,y2bin,z2bin,surfacemap,1), omin=hmin, $
  reverse_indices=ri)]

undefine, x2bin
undefine, y2bin
undefine, z2bin

xoff = 1 - 2*((xoff-xbin) lt 0.5)
yoff = 1 - 2*((yoff-ybin) lt 0.5)
zoff = 1 - 2*((zoff-zbin) lt 0.5)

<span class="comments">; loop through all neighbouring cells in correct order</span>
for xi=0,1 do begin
  for yi=0,1 do begin
    for zi=0,1 do begin
      b = wsrad_map(ngrid, xbin+xi*xoff, ybin+yi*yoff, zbin+zi*zoff, $
        surfacemap, 1)

      <span class="comments">; dual histogram method, loop by count in search bins (see JD's code)</span>
      h2 = histogram(h[(b-hmin+1) > 0], omin=om, reverse_indices=ri2)

      <span class="comments">; loop through repeat counts</span>
      for k=long(om eq 0), n_elements(h2)-1 do if h2[k] gt 0 then begin
        these_bins = ri2[ri2[k]:ri2[k+1]-1]

        if k+om eq 1 then begin <span class="comments">; single point</span>
          these_points = ri[ri[b[these_bins]-hmin]]
        endif else begin
          targ=[h2[k],k+om]
          these_points = ri[ri[rebin(b[these_bins]-hmin,targ,/sample)]+ $
            rebin(lindgen(1,k+om),targ,/sample)]
          these_bins = rebin(temporary(these_bins),targ,/sample)
        endelse

        <span class="comments">; figure out which ones are really within</span>
        these_dist = sphdist(ra1[these_bins],dec1[these_bins], $
          ra2[these_points],dec2[these_points],/degree)
        within = where(these_dist le sphrad, nwithin)
        if nwithin gt 0 then begin
          <span class="comments">; have there been any pairs yet?</span>
          if n_elements(plausible) eq 0 then begin
            plausible = [[these_bins[within]],[these_points[within]],[these_dist[within]]]
          endif else begin
            <span class="comments">; concatenation is inefficient, but we do it at most 8 x N1 times</span>
            plausible = [plausible,[[these_bins[within]],[these_points[within]],$
              [these_dist[within]]]]
          endelse
        endif

      endif
    endfor
  endfor
endfor

if n_elements(plausible) eq 0 then begin
  nwithin=replicate(0l,n1)
  return, replicate(-1,n1+1)
endif else begin
  <span class="comments">; use histogram to generate a reverse_indices array that contains</span>
  <span class="comments">; the relevant entries, and then map into the appropriate elements</span>
  <span class="comments">; in 2</span>
  nwithin = histogram(plausible[*,0], min=0, max=n1-1, reverse_indices=npri)
  distance = plausible[npri[n1+1:*],2]
  npri[n1+1] = plausible[npri[n1+1:*],1]
  return, npri
endelse

end

</code>
    </div>
  </body>
</html>