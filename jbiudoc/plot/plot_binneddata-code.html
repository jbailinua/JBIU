<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
 "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">

<!-- Generated by IDLdoc 3.4 on Fri Jul 29 15:39:45 2016 -->

<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
  <head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8"/>
    <title>plot_binneddata.pro (Documentation for jbiu/)</title>

    
    <link rel="stylesheet" type="text/css" media="all"
          href="../idldoc-resources/main.css" />
    <link rel="stylesheet" type="text/css" media="print"
          href="../idldoc-resources/main-print.css" />
    

    <script type="text/javascript">
      function setTitle() {
        parent.document.title="plot_binneddata.pro (Documentation for jbiu/)";
      }
    </script>
  </head>

  <body onload="setTitle();" id="root">
    <div class="content">
      <code class="source"><span class="comments">;+</span>
<span class="comments">; NAME:</span>
<span class="comments">;    PLOT_BINNEDDATA</span>
<span class="comments">;</span>
<span class="comments">; PURPOSE:</span>
<span class="comments">;    Takes a set of X,Y data and plots the mean y value for points</span>
<span class="comments">;    binned by their x coordinate.</span>
<span class="comments">;</span>
<span class="comments">; CATEGORY:</span>
<span class="comments">;    Plot</span>
<span class="comments">;</span>
<span class="comments">; CALLING SEQUENCE:</span>
<span class="comments">;    PLOT_BINNEDDATA, X, Data, Bin</span>
<span class="comments">;</span>
<span class="comments">; INPUTS:</span>
<span class="comments">;    X:     Array containing x coordinates of data points.</span>
<span class="comments">;</span>
<span class="comments">;    Data:  Array containing y coordinates of data points.</span>
<span class="comments">;</span>
<span class="comments">;    Bin:   Bin width, in x coordinates (if positive) or in</span>
<span class="comments">;           data points per bin (if negative).</span>
<span class="comments">;</span>
<span class="comments">; KEYWORD PARAMETERS:</span>
<span class="comments">;    MIN:       Minimum value for bins. Default: minimum value of X.</span>
<span class="comments">;</span>
<span class="comments">;    MAX:       Maximum value for bins. Default: maximum value of X.</span>
<span class="comments">;</span>
<span class="comments">;    OX:        Optional output of x coordinates of bins.</span>
<span class="comments">;</span>
<span class="comments">;    ODATA:     Optional output of plotted y values.</span>
<span class="comments">;</span>
<span class="comments">;    OERR:      Optional output 2xNbin array containg the ends of the</span>
<span class="comments">;               error bars.</span>
<span class="comments">;</span>
<span class="comments">;    DX:        Shifts the x coordinates of the plot locations (only on</span>
<span class="comments">;               the plot, not in OX).</span>
<span class="comments">;</span>
<span class="comments">;    ERRORBAR:  If /ERRORBAR is set, then error bars are plotted in the y</span>
<span class="comments">;               direction. The size of the error bar is the statistical</span>
<span class="comments">;               error in the mean (or, if /MEDIAN is set, the range</span>
<span class="comments">;               containing the fraction CONFLIMIT of the data).</span>
<span class="comments">;</span>
<span class="comments">;    CONFLIMIT: Fraction of the data to enclose in the error bar if</span>
<span class="comments">;               /MEDIAN is set. For example, use CONFLIMIT=0.9 to</span>
<span class="comments">;               plot the 5th to 95th percentile range.</span>
<span class="comments">;</span>
<span class="comments">;    MEDIAN:    If /MEDIAN is set then plot the median Y value instead</span>
<span class="comments">;               of the mean.</span>
<span class="comments">;</span>
<span class="comments">;    ROBUST:    If /ROBUST is set then use ROBUST_MEAN to calculate the</span>
<span class="comments">;               mean Y value. Not compatible with /MEDIAN.</span>
<span class="comments">;</span>
<span class="comments">;    HORIZONTALBAR:   If /HORIZONTALBAR is set then the routine plots a</span>
<span class="comments">;                     horizontal line that spans each bin.</span>
<span class="comments">;</span>
<span class="comments">;    OVERPLOT:  If /OVERPLOT is set then overplot binned data points on</span>
<span class="comments">;               an existing plot.</span>
<span class="comments">;</span>
<span class="comments">;    NOPLOT:    If /NOPLOT is set then do not create a plot. Useful if</span>
<span class="comments">;               you just want to extract the values using the O...</span>
<span class="comments">;               keywords.</span>
<span class="comments">;</span>
<span class="comments">;   COLOR:      Color of plotted points and error bars.</span>
<span class="comments">;</span>
<span class="comments">;   HISTOGRAM:  Outputs the histogram of number of Data values that</span>
<span class="comments">;               contributed to each bin. Not compatible with negative</span>
<span class="comments">;               values for Bin.</span>
<span class="comments">;</span>
<span class="comments">;   REVERSE_INDICES:  Outputs the reverse indices of the histogram.</span>
<span class="comments">;                     Not compatible with negative values for Bin.</span>
<span class="comments">;</span>
<span class="comments">;   WINDOW:     Sets the /WINDOW keyword in Coyote Graphics.</span>
<span class="comments">;</span>
<span class="comments">;   ADDCMD:     Sets the /ADDCMD keyword in Coyote Graphics.</span>
<span class="comments">;</span>
<span class="comments">;   _Extra: Extra keywords are passed through to CGPLOT.</span>
<span class="comments">;</span>
<span class="comments">; EXAMPLE:</span>
<span class="comments">;    x = 0.1*FINDGEN(20)</span>
<span class="comments">;    y = 10. * x^2 + RANDOMN(seed, 20)</span>
<span class="comments">;    PLOT, PSYM=3, x, y</span>
<span class="comments">;    PLOT_BINNEDDATA, x, y, -4, /OVERPLOT, /ERRORBAR</span>
<span class="comments">;</span>
<span class="comments">; MODIFICATION HISTORY:</span>
<span class="comments">;    Written by:    Jeremy Bailin</span>
<span class="comments">;    12 June 2008   Public release in JBIU</span>
<span class="comments">;    3 May 2009     When BIN&lt;0, make all bins have approximately the</span>
<span class="comments">;                   same number of elements instead of sticking any</span>
<span class="comments">;                   excess elements in the final bin.</span>
<span class="comments">;    12 July 2010   Added /ROBUST keyword.</span>
<span class="comments">;    31 July 2010   Added HISTOGRAM and REVERSE_INDICES keywords.</span>
<span class="comments">;    11 March 2011  Switched to Coyote Graphics.</span>
<span class="comments">;-</span>
<a id="plot_binneddata:source"></a>pro plot_binneddata, X, DATA, BIN, MIN=minval, MAX=maxval, $
 ERRORBAR=errorbarp, OVERPLOT=overplotp, NOPLOT=noplotp, $
 OX=ox, ODATA=odata, OERR=oerr, COLOR=color, DX=dx, MEDIAN=medianp, $
 HORIZONTALBAR=horizontalbarp, CONFLIMIT=conflimit, ROBUST=robustp, $
 HISTOGRAM=Xhist, REVERSE_INDICES=xri, _EXTRA=keywords, $
 WINDOW=window, ADDCMD=addcmd

if keyword_set(medianp) and keyword_set(errorbarp) and $
  n_elements(conflimit) eq 0 then $ 
  message, 'If /MEDIAN and /ERRORBAR are both set, then CONFLIMIT must also be set.'
if keyword_set(medianp) and keyword_set(robustp) then $
  message, '/MEDIAN and /ROBUST cannot both be set.'

if n_elements(minval) eq 0 then minval = min(X)
if n_elements(maxval) eq 0 then maxval = max(X)
if n_elements(color) ne 0 then $
  augment_inherited_keyword, keywords, 'COLOR', color

n = n_elements(X)
if n ne n_elements(data) then message, 'X and DATA do not have the same number of elements.'

if bin gt 0 then begin
  Xhist = histogram(X, min=minval, max=maxval, bin=bin, reverse=xri, location=xloc)
  ox = xloc + 0.5*bin
  nbin = ceil( (float(maxval)-minval)/bin )
endif else begin
  xsort = sort(X)
  nbin = ceil(float(n) / (-bin))
  ox = fltarr(nbin)
endelse

odata = fltarr(nbin)
oerr = fltarr(2,nbin)
xbar = fltarr(2,nbin)
for i=0l,nbin-1 do begin
  if bin gt 0 then begin
    if Xhist[i] eq 0 then begin
      odata[i] = !values.f_nan
      oerr[i] = !values.f_nan
      continue
    endif
    xuse = xri[xri[i]:xri[i+1]-1]
    xbar[0,i] = xloc[i]
    xbar[1,i] = xloc[i]+bin
  endif else begin
    xuse = xsort[floor(i*n/float(nbin)) : floor((i+1)*n/float(nbin))-1]
    ox[i] = median(X[xuse])
    xbar[*,i] = minmax(X[xuse])
  endelse

  if keyword_set(medianp) then begin
    odata[i] = median(DATA[xuse])
    oerr[*,i] = percentiles(DATA[xuse], conflimit=conflimit)
  endif else begin
    if keyword_set(robustp) then begin
      resistant_mean, DATA[xuse], 3., rmean
      odata[i] = rmean
    endif else odata[i] = mean(DATA[xuse])
    if Xhist[i] gt 1 then errsize = stddev(DATA[xuse]) / sqrt(n_elements(xuse))$
      else errsize = 0.
    oerr[0,i] = odata[i]-errsize
    oerr[1,i] = odata[i]+errsize
  endelse
endfor

xplot = ox
if n_elements(dx) gt 0 then xplot += dx

if ~keyword_set(noplotp) then begin
  if ~keyword_set(horizontalbarp) then begin
    cgplot, xplot, odata, overplot=overplotp, window=window, addcmd=addcmd, _extra=keywords
    if keyword_set(window) then begin
      <span class="comments">; switch to addcmd</span>
      window=0
      addcmd=1
    endif
  endif else begin
    if ~keyword_set(overplotp) then begin
      cgplot, [0],[0],/nodata, xrange=minmax(xbar), yrange=minmax(odata), $
	window=window, addcmd=addcmd, _extra=keywords
      if keyword_set(window) then begin
        <span class="comments">; switch to addcmd</span>
        window=0
        addcmd=1
      endif
    endif
   
    if keyword_set(addcmd) then cgcontrol, execute=0 
    for i=0l,nbin-1 do cgplot, /over, xbar[*,i], odata[i]*[1,1], window=window, addcmd=addcmd, _extra=keywords
  endelse

  if keyword_set(errorbarp) then begin
    if keyword_set(addcmd) then cgcontrol, execute=0 
    for i=0l,nbin-1 do cgplot, /over, xplot[i]*[1,1], oerr[*,i], window=window, $
      addcmd=addcmd, _extra=keywords
  endif
endif

if keyword_set(addcmd) then cgcontrol, execute=1

end

</code>
    </div>
  </body>
</html>